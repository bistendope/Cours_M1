Fumeur_ab = [a, b] {
	etat = 4;
	init = 0;

	1 = a;
	2 = b;
	3 = a, b;

	0 -> 1 [P_a];
	0 -> 2 [P_b];
	1 -> 3 [P_b];
	2 -> 3 [P_a];
	3 -> 0 [fume];

};;

Fumeur_ac = [a, c] {
	
	etat = 4;
	init = 0;

	1 = a;
	2 = c;
	3 = a, c;

	0 -> 1 [P_a];
	0 -> 2 [P_c];
	1 -> 3 [P_c];
	2 -> 3 [P_a];
	3 -> 0 [fume];

};;

Fumeur_bc = [b, c] {
	
	etat = 4;
	init = 0;

	1 = b;
	2 = c;
	3 = b, c;

	0 -> 1 [P_b];
	0 -> 2 [P_c];
	1 -> 3 [P_c];
	2 -> 3 [P_b];
	3 -> 0 [fume];

};;

Agent = [] {
	
	etat = 2;
	init = 0;

	0 -> 1 [distr];
	1 -> 0 [signal];

};;

Sem = [libre] {
	
	etat = 2;
	init = 0;

	1 = libre;

	0 -> 1 [V];
	1 -> 0 [P];

};;

Systeme = <Fumeur_ab f1, Fumeur_ac f2, Fumeur_bc f3, Agent agent, Sem a, Sem b, Sem c> {

	<_, _, _, distr, V, V, _>;
	<_, _, _, distr, V, _, V>;
	<_, _, _, distr, _, V, V>;
	
	<P_a, _, _, _, P, _, _>;
	<P_b, _, _, _, _, P, _>;
	<_, P_a, _, _, P, _, _>;
	<_, P_c, _, _, _, _, P>;
	<_, _, P_b, _, _, P, _>;
	<_, _, P_c, _, _, _, P>;

	/*<fume, _, _, signal, _, _, _>;
	<_, fume, _, signal, _, _, _>;
	<_, _, fume, signal, _, _, _>; */

};;

Systeme += prop <- EF(f1.a && f1.b);;
todot etats_acc1.dot Systeme;;
Systeme += prop <- EF(f2.a && f2.c);;
todot etats_acc2.dot Systeme;;
Systeme += prop <- EF(f3.b && f3.c);;
todot etats_acc3.dot Systeme;;


